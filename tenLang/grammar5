<Text> ::=
	<DeclarationBlock> "program:" <ExecCode> ;
<ExecCode> ::=
    <ConsecCode> |
    <LoopCode> |
    <ParallelCode> |
	<Body> |
	[EMPTY] ;
<ConsecCode> ::=
	<ExecCode> ";" <ExecCode> ;
<ParallelCode> ::=
    <Braces> "U" <Braces> ;
<Braces> ::=
    "(" <ExecCode> ")" ;
<LoopCode> ::=
	"(" <ExecCode> ")*" ;
<Body> ::=
	<Assignment> |
	<Test> |
	<Operator> ;
<Assignment> ::=
	<Variable> ":=" <Expression> ;
<Test> ::=
    "(" <Condition> ")?"  ;
<Condition> ::=
	<Expression> ">" <Expression> |
	<Expression> "<" <Expression> |
	<Expression> "=" <Expression> |
	<Expression> ">=" <Expression> |
	<Expression> "<=" <Expression> ;
<Operator> ::= 
	"print" [VARIABLE] |
	"input" [VARIABLE] |
	"upd" [VARIABLE] "[" [CONSTANT] "]" |
	"app" [VARIABLE] "[" [CONSTANT] "]" ;
<DeclarationBlock> ::=
    <DeclarationBlock> "," <DeclarationBlock> |
    <Declaration> |
    [EMPTY] ;
<Declaration> ::=
	"var" <Variable> ":" <Type> ;
<Variable> ::=
    [VARIABLE] ;
<Type> ::=
	"arrayOf" <Type> |
	"int" ;
<Expression> ::=
    <Sum> |
    <Sub> |
	<Term> ;
<Sum> ::=
	<Term> "+" <Term> ;
<Sub> ::=
	<Term> "-" <Term> ;
<Term> ::=
    <Mul> |
    <Div> |
	<Factor> ;
<Mul> ::=
	<Factor> "*" <Factor> ;
<Div> ::=
	<Factor> "/" <Factor> ;
<Factor> ::=
    <FactorPow> |
	<Power> ;
<FactorPow> ::=
	<Power> "^" <Factor> ;
<Power> ::=
    <Negative> |
	<Atom> ;
<Negative> ::=
	"-" <Atom> ;
<Atom> ::=
	[CONSTANT] |
	[VARIABLE] |
	"(" <Expression> ")" ;



