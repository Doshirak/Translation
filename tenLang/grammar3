<Text> ::=
	<DeclarationBlock> "program:" <ExecCode> ;
<ExecCode> ::=
    <ConsecCode> |
    <ParallelCode> |
    <LoopCode> |
	<Body> |
	[EMPTY] ;
<ConsecCode> ::=
	<ExecCode> ";" <ExecCode> ;
<ParallelCode> ::=
	"(" <ExecCode> ")" "U" "(" <ExecCode> ")" ;
<LoopCode> ::=
	"(" <ExecCode> ")*" ;
<Body> ::=
	<Assignment> |
	<Test> |
	<Operator> ;
<Assignment> ::=
	[VARIABLE] ":=" <Expression> ;
<Test> ::=
    "(" <Condition> ")" "?" "{" <ExecCode> "}" ;
<Condition> ::=
	<Expression> ">" <Expression> |
	<Expression> "<" <Expression> |
	<Expression> "=" <Expression> |
	<Expression> ">=" <Expression> |
	<Expression> "<=" <Expression> ;
<Operator> ::= 
	"print" [VARIABLE] |
	"input" [VARIABLE] |
	"upd" [VARIABLE] "[" [CONSTANT] "]" |
	"app" [VARIABLE] "[" [CONSTANT] "]" ;
<DeclarationBlock> ::=
    <DeclarationBlock> "," <DeclarationBlock> |
    <Declaration> |
    [EMPTY] ;
<Declaration> ::=
	"var" [VARIABLE] ":" <Type> ;
<Type> ::=
	"array" "of" <Type> |
	"int" ;
<Expression> ::=
    <Sum> |
    <Sub> |
	<Term> ;
<Sum> ::=
	<Term> "+" <Term> ;
<Sub> ::=
	<Term> "-" <Term> ;
<Term> ::=
    <Mul> |
    <Div> |
	<Factor> ;
<Mul> ::=
	<Factor> "*" <Factor> ;
<Div> ::=
	<Factor> "/" <Factor> ;
<Factor> ::=
    <FactorPow> |
	<Power> ;
<FactorPow> ::=
	<Power> "^" <Factor> ;
<Power> ::=
    <Negative> |
	<Atom> ;
<Negative> ::=
	"-" <Atom> ;
<Atom> ::=
	[CONSTANT] |
	[VARIABLE] |
	"(" <Expression> ")" ;



